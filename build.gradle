plugins {
    id "fabric-loom" version "1.3-SNAPSHOT"
    id "maven-publish"
    id 'io.github.juuxel.loom-quiltflower' version '1.9.+'
}

version = "$mod_version+build.$build_number-$project.minecraft_version"
group = project.maven_group

base {
    archivesName = project.archives_base_name
}

loom {
    accessWidenerPath = file("src/main/resources/vmce.accesswidener")
}

repositories {
    mavenCentral()
    mavenLocal()
    maven { url "https://jitpack.io" }
    maven { url "https://maven.shedaniel.me/" }
    maven { url 'https://maven.wispforest.io' }
    maven { url = "https://maven.isxander.dev/releases" }
    maven { url = 'https://maven.quiltmc.org/repository/release' }
    maven { url = 'https://maven.quiltmc.org/repository/snapshot' }
    maven {
        name = 'ParchmentMC'
        url = 'https://maven.parchmentmc.org'
    }
    maven { url = "https://maven.terraformersmc.com/" }
    maven { url = "https://pkgs.dev.azure.com/djtheredstoner/DevAuth/_packaging/public/maven/v1" }
    maven { url = "https://oss.sonatype.org/content/repositories/snapshots" }
}

configurations.configureEach {
    resolutionStrategy {
        force("net.fabricmc.fabric-api:fabric-api:0.89.0+1.20.1")
        force("net.fabricmc:fabric-loader:0.14.22")
        force("com.terraformersmc:modmenu:7.2.2")
    }
}

dependencies {
    minecraft libs.minecraft
    mappings loom.layered {
        it.mappings("org.quiltmc:quilt-mappings:${libs.versions.quilt.mappings.get()}:intermediary-v2")
        it.parchment("org.parchmentmc.data:parchment-1.20.1:${libs.versions.parchment.get()}@zip")
        it.officialMojangMappings()
    }
    modImplementation libs.fabric.loader
    modImplementation (libs.bundles.fabric)

    modRuntimeOnly ("com.terraformersmc:modmenu:7.2.2")

    modImplementation 'dev.isxander.yacl:yet-another-config-lib-fabric:3.1.1+1.20'

    implementation include('org.json:json:20220320')
    implementation include("com.github.jagrosh:DiscordIPC:-SNAPSHOT")

    modRuntimeOnly("me.djtheredstoner:DevAuth-fabric:1.1.2")
}

processResources {
    inputs.property "version", project.version
    filteringCharset "UTF-8"

    filesMatching("fabric.mod.json") {
        expand "version": project.version
    }
}

tasks.withType(JavaCompile).configureEach {
    it.options.encoding = "UTF-8"
    it.options.getRelease().set(17)
}

java {
    withSourcesJar()
    toolchain.getLanguageVersion().set(JavaLanguageVersion.of(17))
}

jar {
    from("LICENSE") {
        rename { "${it}_${base.archivesName.get()}" }
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            from components.java
        }
    }
    repositories {}
}
